;;; majutsu-sparse-test.el --- Tests for .jjsparse editing  -*- lexical-binding: t; -*-

;; Copyright (C) 2025-2026 0WD0

;; Author: 0WD0 <me@0wd0.com>
;; Maintainer: 0WD0 <me@0wd0.com>
;; Keywords: tools, vc
;; URL: https://github.com/0WD0/majutsu

;; SPDX-License-Identifier: GPL-3.0-or-later

;;; Commentary:

;; Tests for .jjsparse comment handling and font-lock.

;;; Code:

(require 'ert)
(require 'cl-lib)
(require 'majutsu-sparse)

(defun majutsu-sparse-test--faces-at (pos)
  "Return a list of font-lock faces at POS."
  (let ((font-lock-face (get-text-property pos 'font-lock-face))
        (face (get-text-property pos 'face)))
    (cl-remove-duplicates
     (append
      (cond
       ((listp font-lock-face) font-lock-face)
       ((symbolp font-lock-face) (list font-lock-face))
       (t nil))
      (cond
       ((listp face) face)
       ((symbolp face) (list face))
       (t nil)))
     :test #'eq)))

(ert-deftest majutsu-sparse-jjsparse-font-lock-comments ()
  "Only JJ: lines are highlighted as comments."
  (with-temp-buffer
    (let ((with-editor-show-usage nil))
      (majutsu-jjsparse-mode))
    (insert "JJ: keep this as a comment\n")
    (insert "# not a comment\n")
    (insert "src/\n")
    (font-lock-ensure)
    (goto-char (point-min))
    (should (memq 'font-lock-comment-face
                  (majutsu-sparse-test--faces-at (point))))
    (search-forward "# not a comment")
    (beginning-of-line)
    (should-not (memq 'font-lock-comment-face
                      (majutsu-sparse-test--faces-at (point))))))

(ert-deftest majutsu-sparse-directory-candidates ()
  "Directory candidates include top-level and nested paths."
  (cl-letf (((symbol-function 'majutsu-sparse--available-paths)
             (lambda () '("src/a.el" "lib/b.el" "dir/sub/c.el" "README.md"))))
    (let* ((candidates (majutsu-sparse--directory-candidates))
           (sorted (sort (copy-sequence candidates) #'string<)))
      (should (equal sorted
                     (sort (copy-sequence '("src" "lib" "dir/sub" "."))
                           #'string<))))))

(ert-deftest majutsu-sparse-read-patterns/uses-history-and-category ()
  "Sparse pattern reader should expose path-like completion metadata."
  (let (seen-history seen-category)
    (cl-letf (((symbol-function 'completing-read-multiple)
               (lambda (_prompt collection _predicate _require-match _initial history _default)
                 (setq seen-history history
                       seen-category (plist-get completion-extra-properties :category))
                 (should (equal collection '("src" "lib")))
                 '("src"))))
      (should (equal (majutsu-sparse--read-patterns "Sparse" '("src" "lib"))
                     '("src")))
      (should (eq seen-history 'majutsu-sparse-pattern-history))
      (should (eq seen-category 'majutsu-file)))))

(provide 'majutsu-sparse-test)

;;; majutsu-sparse-test.el ends here
